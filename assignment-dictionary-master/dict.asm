global find_word
extern string_equals

section .text

;Пройдёт по всему словарю в поисках подходящего ключа. 
;Если подходящее вхождение найдено, вернёт адрес начала
;вхождения в словарь (не значения), иначе вернёт 0.
;Параметры:
;	rdi: Указатель на нуль-терминированную строку.
;	rsi: Указатель на начало словаря.
;Вывод:
;.если успех:	rax: адрес начала вхождения в словарь
;.иначе:	rax: 0
find_word:
	mov rax, 0		;Очищаем rax
.loop:
	cmp rsi, 0		;Проверяем что словарь не закончился
	je .no_find		;Если закончился - переход
	push rdi		
	push rsi
	add rsi, 8		;Переходим к ключу
	call string_equals	;Проверяем, равны ли строки (ключ?=введенному слову)
	pop rsi
	pop rdi
	cmp rax, 1		;Проверяем равны ли строки
	je .find		;Если равны, то переход
	mov rsi, [rsi]		;Передвигаем ссылку на следующий элемент
	jmp .loop		;Переходим к проверке следующего элемента
.no_find:
	mov rax, 0		;Кладем 0, т.к. элемент не найден
	ret
.find:
	mov rax, rsi		;Кладем адрес ключа элемента
	ret
